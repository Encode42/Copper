plugins {
    id("java")
    id("maven-publish")
    id("com.github.johnrengelman.shadow") version "7.1.0"
}

allprojects {
    group = "dev.encode42.copper"
    version = "2.0-SNAPSHOT"
    description = "Java API held together by tape"

    repositories {
        mavenCentral()
        maven {
            url = "https://jitpack.io"
            name = "jitpack"
        }
    }

    java {
        targetCompatibility = JavaVersion.toVersion(17)
        sourceCompatibility = JavaVersion.toVersion(17)

        withJavadocJar()
        withSourcesJar()
    }

    test {
        useJUnitPlatform()
        testLogging {
            showStandardStreams = true
        }
    }
}

subprojects {
    if (name == "copper-bom") {
        apply plugin: "java-platform"
    } else {
        apply plugin: "java-library"
    }

    apply plugin: "maven-publish"
    apply plugin: "com.github.johnrengelman.shadow"

    publishing {
        repositories {
            maven {
                def releases = "https://repo.encode42.dev/releases"
                def snapshots = "https://repo.encode42.dev/snapshots"

                name = "encode42Repo"
                url = version.endsWith("SNAPSHOT") ? snapshots : releases
                credentials(PasswordCredentials)
            }
        }
        publications {
            "$project.name"(MavenPublication) {
                if (project.name == "copper-bom") {
                    from components.javaPlatform
                } else {
                    from components.java
                }
            }
        }
    }
}

dependencies {
    testImplementation(group: "org.junit.jupiter", name: "junit-jupiter-api", version: "5.8.1")
    testRuntimeOnly(group: "org.junit.jupiter", name: "junit-jupiter-engine", version: "5.8.1")
}
